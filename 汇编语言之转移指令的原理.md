---
title: 汇编语言之转移指令的原理
date: 2021-07-17 20:44:10
author: MZJ
---
# 转移指令的原理
## 引言
### 8086CPU的转移指令分为以下指令：
1.无条件转移指令（如jmp）  
2.条件转移指令  
3.循环指令（如loop）  
4.过程（函数）  
5.中断
## 操作符offset
操作符offset在汇编语言中是由编译器处理的符号，它的功能是取得标号的偏移地址。  
## jmp指令
jmp指令为无条件转移指令，可以修改IP，也可以同时修改cs和IP。  
jmp指令要给出两种信息：  
转移的目的地址  
转移的距离（段间转移，段内短转移，段内近转移）  
## 依据位移进行转移的jmp指令
### jmp short 标号
转到标号处执行指令  
这种格式实现的是段内短转移，它对IP修改的范围为-128 ~ 127，也就是说它向前转移最多可以越过128个字节，向后转移最多越过127个字节。  
#### 我们写一个简单的程序
![contents](https://github.com/MzjHarley/AssemblyLanguageBasicKnowledge/blob/main/img/%E6%B1%87%E7%BC%96%E8%AF%AD%E8%A8%80%E4%B9%8B%E8%BD%AC%E7%A7%BB%E6%8C%87%E4%BB%A4%E7%9A%84%E5%8E%9F%E7%90%86/1.png)  
汇编指令jmp short s对应的机器指令是什么样呢？  
我们先看一下一般的汇编指令对应的机器码是什么样子。  
```
汇编指令          机器指令  
mov ax,0123       B8 23 01
mov ax,ds:[0123]  A1 23 01
push ds:[0123]    FF 36 23 01
```
可以看出在一般的汇编指令中，指令中的立即数（idata），不论是表示一个数据还是内存单元的偏移地址，都会在对应的机器指令中出现，因为CPU处理的是机器指令，它必须要处理这些数据或地址。  
![contents](https://github.com/MzjHarley/AssemblyLanguageBasicKnowledge/blob/main/img/%E6%B1%87%E7%BC%96%E8%AF%AD%E8%A8%80%E4%B9%8B%E8%BD%AC%E7%A7%BB%E6%8C%87%E4%BB%A4%E7%9A%84%E5%8E%9F%E7%90%86/2.png)  
我们可以看到debug将**jmp short s**中的**s**表示为**inc ax指令的偏移地址8**，并且将其表示为 **jmp 0008**，表示转移到cs:0008处。  
但是我们观察**jmp 0008**对应的机器码为**EB03**,这个机器码不包含转移的目的地址。也就是说CPU在执行**EB03**时不知道转移的目的地址。  
如果机器指令中不包含目的地址的话，也就是说CPU不需要这个目的地址就可以实现对IP的修改。  
#### 那么CPU如何实现对IP的修改？
##### 一
![contents](https://github.com/MzjHarley/AssemblyLanguageBasicKnowledge/blob/main/img/%E6%B1%87%E7%BC%96%E8%AF%AD%E8%A8%80%E4%B9%8B%E8%BD%AC%E7%A7%BB%E6%8C%87%E4%BB%A4%E7%9A%84%E5%8E%9F%E7%90%86/3.png)  
**jmp short s**指令的读取和执行过程：  
1.CS:IP指向机器码EB 03  
2.读取的指令码EB 03进入指令缓冲器中  
3.(IP)=(IP)+所读取指令的长度=（IP）+2=0005,CS:IP指向add ax,0001  
4.CPU执行指令缓冲器中的指令EB 03  
5.执行EB 03后，(IP)=0005H+机器码03所对应的真值（十进制数）=0008H（16进制和十进制进行运算要转换为相同进制）,CS:IP指向inc ax
##### 二 
![contents](https://github.com/MzjHarley/AssemblyLanguageBasicKnowledge/blob/main/img/%E6%B1%87%E7%BC%96%E8%AF%AD%E8%A8%80%E4%B9%8B%E8%BD%AC%E7%A7%BB%E6%8C%87%E4%BB%A4%E7%9A%84%E5%8E%9F%E7%90%86/4.png)  
**jmp s**指令的读取和执行过程：  
1.CS:IP指向机器码EB F6  
2.读取的指令码EB F6进入指令缓冲器中  
3.(IP)=(IP)+所读取指令的长度=（IP）+2=000A,CS:IP指向mov bx,1  
4.CPU执行指令缓冲器中的指令EB F6  
5.执行EB F6后，(IP)=000AH+机器码F6所对应的真值（十进制数）=0000H（16进制和十进制进行运算要转换为相同进制）,CS:IP指向mov ax,4c00H  
F6在计算机内部以补码的形式存储：1111 0110，它表示的真值为-10。  
**补码求真值：  
1.如果为负数，以F6为例，它的二进制为1111 0110，由二进制最高位确定符号位为负数，随后取反加一（包括符号位）得到00001001得到十进制数10，将符号位和得到的值合并起来为-10。  
2.如果为正数，则这个二进制表示的十进制就是真值。**  
实际上 jmp short 标号的功能为（IP）=（IP）+8位位移  
(1)8位位移=标号处的地址-jmp指令后的第一个字节的地址  
(2)short指明此处的位移为8位位移  
(3)8位位移的范围为-128 ~ 127，用补码表示  
(4)8位位移由编译程序在编译时算出。
### jmp near ptr标号
它实现段内近转移。  
jmp near ptr标号功能为（IP）=（IP）+16位位移  
1)16位位移=标号处的地址-jmp指令后的第一个字节的地址  
(2)near ptr指明此处的位移为16位位移  
(3)16位位移的范围为-32769 ~ 32767，用补码表示  
(4)16位位移由编译程序在编译时算出。
## 转移的目的地址在指令中的jmp指令
### jmp far ptr 标号
它实现的是段间转移，又称远转移。  
jmp far ptr 标号功能如下：  
1.(CS)=标号所在段的段地址。  
2.(IP)=标号所在段中的偏移地址。  
3.far ptr 指明了指令用标号的段地址和偏移地址修改CS和IP。  
目的地址在指令中的存储顺序：高地址是转移地址的段地址，低地址是转移地址的偏移地址。
## 转移地址在寄存器中的jmp指令
### jmp 16位寄存器
功能： IP=（16位寄存器）
## 转移地址在内存中的jmp指令
### jmp word ptr 内存单元地址（段内转移）
功能：从内存单元地址处开始存放一个字，是转移的目的偏移地址。  
内存单元地址可用寻址方式的任意格式给出。  
如：  
mov ax,0123H  
mov ds:[0],ax  
jmp word ptr ds:[0]相当于jmp ax  
执行之后(IP)=0123H  

### jmp dword ptr 内存单元地址（段间转移）
功能：从内存单元地址处开始存放两个字，高地址处的字是转移的目的段地址，低地址处的字是转移的目的偏移地址。  
(CS)=（内存单元地址+2）  
(IP)=（内存单元地址）  
内存单元地址可用寻址方式的任意格式给出。  
如  
mov ax,0123H  
mov ds:[0],ax  
mov word ptr ds:[2],0  
jmp dword ptr ds:[0]  
执行之后(IP)=0123H,(CS)=0  
CS:IP=0000:0123H

## jcxz指令
jcxz指令为有条件转移指令，所有的有条件转移指令都是短转移指令，在对应机器码中包含转移的位移而不是目的地址。对IP的修改范围都为-128 ~ 127。  
指令格式：jcxz 标号  
如果(CX)=0，则转移至标号处执行。  
当(CX)=0时，（IP）=（IP）+8位位移  
(1)8位位移=标号处的地址-jcxz指令后的第一个字节的地址  
(2)8位位移的范围为-128 ~ 127，用补码表示  
(3)8位位移由编译程序在编译时算出。  
当(CX)!=0时，什么也不做。  
jcxz 标号相当于  
if((cx)==0)  
 jmp short 标号
## loop指令
loop指令为循环指令，所有的循环指令都是短转移指令，在对应的机器码中包含转移的位移，而不是目的地址。  
loop 标号相当于  
cx--;  
if((cx)!=0)  
jmp short 标号
## 根据位移进行转移的意义
1.jmp short标号  
2.jmp near ptr标号  
3.jcxz 标号  
4.loop标号  
这四种汇编指令，它们对IP的修改是根据转移目的地址和转移起始地址之间的位移来进行的，在它们的机器码中不包含转移的目的地址，而包含的是到目的地址的位移距离。  
这样设计，是为了程序段在内存中的浮动装配。

## 编译器对转移位移超界的检测
根据位移进行转移的指令，它们的转移范围会受到转移位移的限制，如果源程序中出现了转移范围超界的问题，编译时会报错。  
我们在第二章使用的**jmp 2000:0100**,是在debug中使用的汇编指令，汇编编译器并不认识。如果在源程序中使用，编译会报错。  
## 实验八 
分析下面程序判断这个程序是否能正确返回？  
![contents](https://github.com/MzjHarley/AssemblyLanguageBasicKnowledge/blob/main/img/%E6%B1%87%E7%BC%96%E8%AF%AD%E8%A8%80%E4%B9%8B%E8%BD%AC%E7%A7%BB%E6%8C%87%E4%BB%A4%E7%9A%84%E5%8E%9F%E7%90%86/5.png)  
### 运行得出结果
![contents](https://github.com/MzjHarley/AssemblyLanguageBasicKnowledge/blob/main/img/%E6%B1%87%E7%BC%96%E8%AF%AD%E8%A8%80%E4%B9%8B%E8%BD%AC%E7%A7%BB%E6%8C%87%E4%BB%A4%E7%9A%84%E5%8E%9F%E7%90%86/6.png)  
运行得之程序能够正确返回。
### 逻辑验证结果
采用debug跟踪程序  
![contents](https://github.com/MzjHarley/AssemblyLanguageBasicKnowledge/blob/main/img/%E6%B1%87%E7%BC%96%E8%AF%AD%E8%A8%80%E4%B9%8B%E8%BD%AC%E7%A7%BB%E6%8C%87%E4%BB%A4%E7%9A%84%E5%8E%9F%E7%90%86/7.png)  
mov di,offset s   将标号s的偏移地址放在di中  
mov si,offset s2 将标号s2的偏移地址放在si中  
mov ax,cs:[si]  将cs:[si]内存单元的内容jmp short s1的机器码EB F6放在ax中  
mov cs:[di],ax 将ax的内容EB F6放入cs:[di]内存单元中，即标号s处的内容从**机器码90变成了机器码EB F6**.  
![contents](https://github.com/MzjHarley/AssemblyLanguageBasicKnowledge/blob/main/img/%E6%B1%87%E7%BC%96%E8%AF%AD%E8%A8%80%E4%B9%8B%E8%BD%AC%E7%A7%BB%E6%8C%87%E4%BB%A4%E7%9A%84%E5%8E%9F%E7%90%86/8.png)  
按照程序执行的顺序从start开始执行指令，执行到如下图箭头指向的这一步时，  
![contents](https://github.com/MzjHarley/AssemblyLanguageBasicKnowledge/blob/main/img/%E6%B1%87%E7%BC%96%E8%AF%AD%E8%A8%80%E4%B9%8B%E8%BD%AC%E7%A7%BB%E6%8C%87%E4%BB%A4%E7%9A%84%E5%8E%9F%E7%90%86/9.png)  
1.CS:IP指向机器码EB F0  
2.读取的指令码EB F0进入指令缓冲器中  
3.(IP)=(IP)+所读取指令的长度=（IP）+2=0018,CS:IP指向mov ax,0  
4.CPU执行指令缓冲器中的指令EB F0  
5.执行EB F0后，(IP)=0018H+机器码F0所对应的真值=8D（D表示十进制，16进制和十进制进行运算要转换为相同进制）,CS:IP指向标号S.  
而标号s处的内容是**机器码EB F6**.  
![contents](https://github.com/MzjHarley/AssemblyLanguageBasicKnowledge/blob/main/img/%E6%B1%87%E7%BC%96%E8%AF%AD%E8%A8%80%E4%B9%8B%E8%BD%AC%E7%A7%BB%E6%8C%87%E4%BB%A4%E7%9A%84%E5%8E%9F%E7%90%86/10.png)  
1.CS:IP指向机器码EB F6  
2.读取的指令码EB F6进入指令缓冲器中  
3.(IP)=(IP)+所读取指令的长度=（IP）+2=000AH,CS:IP指向mov di,offset s  
4.CPU执行指令缓冲器中的指令EB F6  
5.执行EB F6后，(IP)=000AH+机器码F6所对应的真值=0000H（16进制和十进制进行运算要转换为相同进制）,CS:IP指向CS:0000，cs:0000处的指令为mov ax,4c00H。  
程序跳转到CS:0000处顺序执行，执行mov ax,4c00H；int 21H后程序正常返回。
## 实验九
在屏幕中间分别显示绿色、绿底红色、白底蓝色的字符串‘welcome to masm!’。
### 80 * 25彩色字符模式显示缓冲区（以下简称显示缓冲区）的结构：
内存地址空间中，B8000H ~ BFFFFH共32KB的空间，为80 * 25彩色字符模式显示缓冲区，向这个地址空间写入数据，会直接显示到显示器上。  
80 * 25 表示显示器可以显示25行，每行80个字符。  
一个字符在显示缓冲区占两个字节，分别存放字符的ASCII码和属性，低位字节存放ASCII码，高位字节存放字符属性。即偶地址存放字符，奇地址存放字符属性。  
故一行占80 * 2=160个字节，一屏的内容占160 * 25=4000B，大约4KB  
显示缓冲区分为32/4=8页，显示器可以显示任意一页，一般显示第0页的内容，即通常情况下显示B8000H（偏移地址为十进制0） ~ B8F9FH（偏移地址为十进制3999）中的内容。  
字符属性格式：  
```
     7 6 5 4 3 2 1 0
含义 BL R G B I R G B
R:红色
G:绿色
B:蓝色
```
第7位决定字符闪烁属性，该属性必须DOS工作台全屏才能看到  
第4 ~ 6位决定字符背景（底色）属性  
第3位决定字符高亮属性  
第1 ~ 3位决定字符前景（字符色）属性  
在相应位填0或1配置不同的前景色和背景色，0表示不具有，1表示具有  
如：  
配置红底字，属性字节为01000000B，16进制表示40H  
配置红底闪烁绿字，属性字节为11000010B，16进制表示C2H
### 程序如下
```assembly
assume cs:codesg,ds:datasg,ss:stacksg

datasg segment
db 'welcome to masm!'
db  2H,24H,71H
datasg ends
;2H,24H,71H分别对应字符属性绿色、绿底红色、白底蓝色的16进制
;定义数据段，用于存放需要显示的字符和字符属性
;该数据段的内容要被显示3次，分别在DOS工作台的第12、13、14行的中间.
;即该数据段的内容要被放在B872:0~B873:1F、;B87C:0~B87D:1F、B886:0~B887:1F三个段中。
;这三个段中的段地址相差0AH

stacksg segment
dw 8 dup(0) 
stacksg ends
;定义堆栈段，用于暂存数据，防止数据被覆盖

codesg segment 
start:
mov ax,datasg 
mov ds,ax

mov ax,stacksg
mov ss,ax
mov sp,16

xor bx,bx                ;相当于mov bx,0.       bx用于索引颜色
mov ax,0B872H            ;ax存放数据段中内容存放位置的段地址，数据不能以字母开头
mov cx,3                 ;三次大循环，每次循环需要做两件事：1.将字符放入相应位置     2.将字符属性放入相应位置

s:
push cx                  ;用到多次循环，为防止cx数据被覆盖，故以压栈暂存数据。
push ax
mov es,ax                ;将ax中的数据放在es段寄存器中

mov cx,16
mov si,0
mov di,0
s1:
mov al,ds:[si]
mov es:[di],al
inc si
add di,2
loop s1
;将字符放入相应位置 ,偶地址存放字符


mov cx,16
mov di,1
s2:
mov al,ds:[bx+16]
mov es:[di],al
add di,2
loop s2
;将字符属性放入相应位置,奇地址存放字符属性


inc bx
pop ax
add ax,0AH ;段地址增加0AH跳到下一行执行循环
pop cx
loop s
;注意pop的顺序,不能乱


mov ax,4c00H
int 21H
codesg ends
end start
```
### 运行结果
![contents](https://github.com/MzjHarley/AssemblyLanguageBasicKnowledge/blob/main/img/%E6%B1%87%E7%BC%96%E8%AF%AD%E8%A8%80%E4%B9%8B%E8%BD%AC%E7%A7%BB%E6%8C%87%E4%BB%A4%E7%9A%84%E5%8E%9F%E7%90%86/11.png)
# 转移指令的原理结束
