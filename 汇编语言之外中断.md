---
title: 汇编语言之外中断
date: 2021-09-04 21:31:26
author: MZJ
---
# 外中断
## 引言
CPU在计算机系统中除了能执行指令，进行运算外，还能够对外部设备进行控制，接受它们的输入，向它们输出。  
**总之，CPU除了具有运算能力还有I/O能力。**

## 接口芯片和端口
在PC系统的接口卡和主板上，装有各种接口芯片。这种外设接口芯片的内部有若干寄存器，CPU将这些寄存器当作端口来访问。  
外设的输入不直接送入CPU，而是送入相关的接口芯片的端口中。  
CPU向外设的输出也不是直接送入外设，而是先送入端口中，再由相关的芯片送入外设。  
CPU还可以向外设输出指令，这些指令先送入相关芯片的端口中，再由相关的芯片根据命令对外设实施控制。  
**总之，CPU通过端口和外设相联系。**  
CPU在完成当前指令后，可以检测到发送过来的中断信息，引发中断过程，处理外设的输入。
## 外中断信息
>在PC系统中，外中断一共有两类：  
>1.可屏蔽中断  
>2.不可屏蔽中断  

可屏蔽中断时是CPU可以不响应的外中断。CPU是否响应可屏蔽中断，主要看标志寄存器的IF位设置。  
当CPU检测到可屏蔽中断信息时：  
如果IF=1,则CPU在执行完当前指令后响应可屏蔽中断，引发中断过程。  
如果IF=0,则CPU在执行完当前指令后不响应可屏蔽中断。  
>内中断的中断过程:  
>1.取得中断类型码N  
>2.pushf，TF=0,IF=0  
>3.push CS  
>4.push IP  
>5.(CS)=(N * 4+2)，(IP)=(N * 4)  

可屏蔽中断引发的中断过程，除在第一步的实现有所不同外，基本上和内中断的中断过程相同。  
因为可屏蔽中断的中断信息来自于CPU外部，中断类型码是通过数据总线送入CPU的。而内中断的中断类型码是在CPU的内部产生的。  
将IF置0原因：在进入中断处理程序后，禁止其他的可屏蔽中断。  
>8086CPU提供了设置IF的指令:  
>sti：将IF置1  
>cli：将IF清0  

不可屏蔽中断是CPU必须响应的外中断。当CPU检测到不可屏蔽中断信息，则在执行完当前指令后，立即响应，引发中断过程。  
对于8086CPU不可屏蔽中断的中断类型码固定为2，所以中断过程中，不需要中断类型码。  
>不可屏蔽中断的中断过程:  
>1.pushf，TF=0,IF=0  
>2.push CS  
>3.push IP  
>5.(CS)=(0AH)，(IP)=(8)  

几乎所有由外设引起的外中断，都是可屏蔽中断。当外设有需要处理的事件(如键盘输入)发生时，相关芯片向CPU发出可屏蔽信息。  
不可屏蔽中断是在系统中有必须处理的紧急情况发生时用来通知CPU的中断信息。  
## PC机键盘的处理过程
>键盘输入处理过程:  
>1.键盘输入  
>2.引发9号中断  
>3.执行int 9中断例程  

### 键盘输入
键盘上的每一个键相当于一个开关，键盘中有一个芯片对键盘上的每一个键的开关状态进行扫描。  
>1.按下一个键，开关接通，该芯片就产生一个扫描码，说明了按下的键在键盘上的位置。  
>2.按下键时产生的扫描码被送入主板上相关接口芯片的寄存器中，该寄存器的端口地址为60H.  
>3.松开按下的键时，也产生一个扫描码，说明了松开的键在键盘上的位置。  
>4.松开键时产生的扫描码也被送入60H端口中。  

一般将按下一个键产生的扫描码称为通码，松开一个键产生的扫描码称为断码。  
扫描码的长度为一个字节，通码的第7位为0，断码的第7位为1，即断码=通码+80H

#### 键盘上部分键的通码
![contents](https://github.com/MzjHarley/AssemblyLanguageBasicKnowledge/blob/main/img/%E6%B1%87%E7%BC%96%E8%AF%AD%E8%A8%80%E4%B9%8B%E5%A4%96%E4%B8%AD%E6%96%AD/1.png)
### 引发9号中断
键盘的输入到达60H端口时，相关的芯片就会向CPU发出中断类型码为9的可屏蔽中断信息。  
CPU检测到该中断信息后，若IF=1，则响应中断，引发中断过程，转去执行int 9中断例程。
### 执行int 9中断例程
BIOS提供了int 9中断例程，用来进行基本的键盘输入处理。  
>int 9中断例程的主要工作：  
>1.读出60H端口的扫描码  
>2.如果是字符键的扫描码，将该扫描码和它对应的字符码(即ASCII码)进入**内存中的BIOS键盘缓冲区**。如果是控制键(如Crtl)或切换键(如Capslock)的扫描码，则将其变成**状态字节(用二进制位记录其状态的字节)写入到内存中存储状态字节的单元**。  
>3.对键盘系统进行相关的控制，如：向相关芯片发送应答信息。  

BIOS键盘缓冲区是系统启动后，BIOS用于存放int 9中断例程所接收的键盘输入的内存区。  
该内存区可以存储15个键盘输入，因为int 9中断例程除了接收扫描码外，还要产生和扫描码对应的字符码，所以在BIOS键盘缓冲区中，**一个键盘输入用一个字单元存放，高位字节存放扫描码，低位字节存放其字符码**。  
0040:17单元存储键盘状态字节，该字节记录了控制键和切换键的状态。  
键盘状态字节各位记录的信息:  
![contents](https://github.com/MzjHarley/AssemblyLanguageBasicKnowledge/blob/main/img/%E6%B1%87%E7%BC%96%E8%AF%AD%E8%A8%80%E4%B9%8B%E5%A4%96%E4%B8%AD%E6%96%AD/2.png)
## 编写int9中断例程
>重新总结键盘输入的处理过程:  
>1.键盘输入产生扫描码。  
>2.扫描码送入到60H端口中。  
>3.键盘输入到达60H后，相关芯片发送中断类型码9到CPU，若IF=1,则引发9号中断过程。  
>4.CPU执行int 9中断例程处理键盘输入。  

以上前三步由硬件系统自动完成，我们能做的改变只有第四步，修改int 9中断程序。
### 任务：在屏幕中间依次显示a ~ z，并让人看清。在显示过程中，按下Esc键后，改变显示的颜色。
>据上述任务我们可以编写int9中断例程，功能如下：  
>从60H端口中读取扫描码  
>调用BIOS的int 9中断例程，处理其他硬件细节。  
>判断是否是Esc的扫描码，如果是，改变颜色后返回；如果不是直接返回。  

#### 程序如下
```assembly
assume cs:code
stack segment
db 128 dup(0)
stack ends
data segment
dw 0,0
data ends
code segment
start:
mov ax,data
mov ds,ax
mov ax,stack
mov ss,ax
mov sp,128
mov ax,0
mov es,ax
;==================保存原来int 9号中断例程的入口地址======================
push es:[4*9]
pop ds:[0]
push es:[4*9+2]
pop ds:[2]
;=========将我们的写的int 9号中断例程的入口地址保存在中断向量表表项中==========
cli    ;设置IF为0，防止在该步骤发生键盘中断，导致错误
mov word ptr es:[4*9],offset int9
mov es:[4*9+2],cs
sti    ;与sti成对使用
;===================显示a~z字符并让人看清===============================
mov ax,0B800H
mov es,ax
mov al,'a'
display:
mov es:[12*160+40*2],al
call delay
inc al
cmp al,'z'
jna display
;=======================恢复原来的int 9号的入口地址=======================
cli    ;设置IF为0，防止在该步骤发生键盘中断，导致错误
mov ax,0
mov es,ax
push ds:[0]
pop es:[4*9]
push ds:[2]
pop es:[4*9+2]
sti    ;与sti成对使用
;================================程序返回==============================
mov ax,4c00H
int 21H
;=========================编写delay子程序==============================
delay:
push ax
push dx
mov dx,10H
mov ax,0
s1:
sub ax,1
sbb dx,0
cmp ax,0
jne s1
cmp dx,0
jne s1

pop dx
pop ax
ret
;===========================新编写的int 9中断例程=======================
int9:
push ax
push bx
push es
;==============从60H读取扫描码==============
in al,60H
;==调用BIOS的int 9号中断例程处理其他硬件细节====
pushf
pushf
pop bx
and bh,11111100B
push bx
popf
call dword ptr ds:[0]
;==========判断是否是Esc的扫描码==============
cmp al,1
jne int9ret

mov ax,0b800H
mov es,ax
inc byte ptr es:[160*12+40*2+1]
;==========================================
int9ret:
pop es
pop bx
pop ax
iret
;==================================================================
code ends
end start
```
#### 程序总结
在执行我们所写的int9号中断程序时，我们是需要在其中调用BIOS提供的int 9中断例程用于处理其他硬件细节。这就牵扯到BIOS提供的int 9中断例程入口地址的保存及调用。由于BIOS提供的int 9中断例程的入口地址在中断向量表的位置被我们所写的int9号中断处理程序的入口地址占用，因此不能通过int 9H的形式调用BIOS提供的int 9中断例程。因此我们需要调用int 9号中断例程的话，需要从其调用过程来做。  
int 9H是外中断中的可屏蔽中断。  
我们清楚int 9H的调用过程：  
>1.pushf  
>2.IF=0,TF=0  
>3.push CS，push IP  
>4.CS=(4 * 9+2)，IP=(4 * 9)  

此时在于将第2步和第3，4步转化：  
第2步将TF和IF=0，即将flag寄存器的第8位和第9位变成0，我们可以采用and和栈来完成。  
第3，4步我们可以用转移地址在内存单元中的call指令来解决。  
这样就可以调用BIOS提供的int 9中断例程了。  
其实我们并不需要设置IF和TF=0，因为在我们所写的int9中断例程发生时，它们已经被硬件自动设置为0了，但我们需要知道怎么去设置。  
因此上述调用BIOS提供的int 9中断例程直接考虑第1步和和第3，4步就行了。  
>调用BIOS提供的int 9中断例程：  
>pushf  
>call dword ptr ds:[0]  

由于CPU的运行速度很快，因此显示在屏幕上的东西会很快消失导致人看不清，因此就需要使用一个delay程序，使其可以停滞，以便观看。  
我们采用执行空循环来实现停滞的效果，我写的程序是执行了100000H次空循环，如果还是不适合你的话，可以适当减少或增加循环次数以达到停滞效果。  

程序返回前需要恢复BIOS提供的int 9中断例程的入口地址在中断向量表的位置，否则程序返回后，别的程序将无法使用键盘。  
## 安装新的int 9中断例程
功能：在DOS下，按F1键后改变当前屏幕的显示颜色，其他的键照常处理。  
```assembly
assume cs:code 

stack segment
db 128 dup(0)
stack ends

code segment
start:
mov ax,stack
mov ss,ax
mov sp,128
;==========================安装int9程序===========================
mov ax,cs
mov ds,ax
mov si,offset int9
mov ax,0
mov es,ax
mov di,204H
mov cx,offset int9end - offset int9
cld
rep movsb
;================保存BIOS提供的int 9中断例程的入口地址================
cli
push es:[4*9]
pop es:[200H]
push es:[4*9+2]
pop es:[202H]
;=================将int9程序的入口地址登记在0:4*9中==================
mov word ptr es:[4*9],204H
mov word ptr es:[4*9+2],0
sti
;===========================程序返回================================
mov ax,4c00H
int 21H
;==========================编写int 9中断例程=========================
int9:
push ax
push es
push cx
push bx

in al,60H

pushf
call dword ptr cs:[200H]

cmp al,3BH         ;F1扫描码为3BH
jne int9ret

mov ax,0B800H
mov es,ax
mov cx,2000
mov bx,1
s:inc byte ptr es:[bx]
add bx,2
loop s

int9ret:
pop bx
pop cx
pop es
pop ax
iret

int9end:nop
;==================================================================
code ends
end start
```
重新总结下CPU对外设输入的通常处理方法：  
>1.外设的输入送入端口。  
>2.向CPU发出外中断(可屏蔽中断)的信息.  
>3.CPU检测到可屏蔽中断的信息，如果IF=1，则CPU在执行完当前指令后响应中断，执行相应的中断例程。  
>4.可在中断例程中实现对外设输入的处理。  

端口和中断机制，是CPU进行I/O的基础。  
## 实验15 安装一个新的int 9中断例程，功能：在DOS下，按A键后，除非不再松开，如果松开就显示满屏的A，其他的键照常处理。
```assembly
assume cs:code

stack segment
db 128 dup(0)
stack ends

code segment
start:
mov ax,stack
mov ss,ax
mov sp,128
;==========================安装int9程序===========================
mov ax,cs
mov ds,ax
mov si,offset int9
mov ax,0
mov es,ax
mov di,204H
mov cx,offset int9end - offset int9
cld
rep movsb
;================保存BIOS提供的int 9中断例程的入口地址================
cli
push es:[4*9]
pop es:[200H]
push es:[4*9+2]
pop es:[202H]
;=================将int9程序的入口地址登记在0:4*9中==================
mov word ptr es:[4*9],204H
mov word ptr es:[4*9+2],0
sti
;===========================程序返回================================
mov ax,4c00H
int 21H
;==========================编写int 9中断例程=========================
int9:
push ax
push es
push cx
push bx

in al,60H

pushf
call dword ptr cs:[200H]

cmp al,1EH+80H         ;F1扫描码为3BH
jne int9ret

mov ax,0B800H
mov es,ax
mov cx,2000
mov bx,0
s:mov byte ptr es:[bx],'A'
add bx,2
loop s

int9ret:
pop bx
pop cx
pop es
pop ax
iret

int9end:nop
;==================================================================
code ends
end start
```
## 指令总结
>1.数据传送指令：  
>mov ,push,pop,pushf,popf,xchg  
>实现寄存器和内存，寄存器和寄存器之间单个数据的传送。  

>2.算数运算指令：  
>add,adc,sub,sbb,inc,dec,mul,,imul,div,idiv,aaa,cmp  
>实现寄存器和内存中数据的算数运算。  
>执行结果影响flag寄存器的标志位  

>3.逻辑指令：  
>and,or,shl,shr,not,xor,test,sal,rol,ror,rcl,rcr,sar  
>除not指令外，其他逻辑指令执行结果影响flag寄存器的标志位  

>4.转移指令：  
>可以同时修改CS和IP，或可以修改IP的指令统称为转移指令。  
>(1).无条件转移指令，jmp  
>(2).条件转移指令，jcxz,je,jne,ja,jna,jb,jnb  
>(3).循环指令，loop  
>(4).过程，call,ret,retf  
>(5)中断，int,iret  

>5.处理机制指令:  
>对寄存器或其他处理机状态进行设置  
>cld,std,cli,sti,nop,clc,cmc,stc,hlt,wait,esc,lock  

>6.串传送指令:  
>对内存中的数据进行批量处理  
>movsb,movsw,cmps,scas,lods,stos  
>若要更方便对内存中的数据进行批量处理，则需和前缀指令rep，repe，repne等配合使用

# 外中断结束