---
title: 汇编语言之端口
date: 2021-09-04 09:45:55
author: MZJ
---
# 端口(PORT)
## 引言
>CPU可以直接读写3个地方的数据：  
>1.CPU内部寄存器  
>2.内存单元  
>3.端口  

## 端口的读写
对端口的读写不能用mov、push、pop等内存读写指令。端口的读写指令只有两条:in和out分别用于从端口读取数据和往端口写入数据。  
### CPU执行内存访问指令和端口访问指令时，总线上的信息
#### 访问内存
例如：mov ax,ds:[8]  
假设执行前(ds)=0  
>执行时，与总线相关的操作：  
>CPU通过地址线将地址信息8发出。（地址信息：要进行读或写的内存单元的位置）  
>CPU通过控制线发出内存读命令，选中存储器芯片，并通知它，将要从中读取数据。（控制信息：器件的选择，发出读或写命令）  
>存储器(内存)将8号内存单元中的数据通过数据线送入CPU。（数据信息：要读或写的数据）  

#### 访问端口
in al,60h  从60H号端口读入一个字节  
>执行时与总线相关的操作：  
>CPU通过地址线将地址信息60H发出。  
>CPU通过控制线发出读命令，选中端口所在芯片，并通知它，将要从中读取数据。  
>端口所在的芯片通过数据线将60h端口中的数据送入CPU。  

#### 注意
在in和out指令中，只能使用ax或al来存放从端口读出或要发送到端口中的数据。  
访问8位端口(一次传输一个字节)用al，访问16位端口(一次传输2个字节)用ax。  
>对0 ~ 255以内的端口进行读写时：  
>in al,20H 从20H号端口中读取一个字节，将其送入al中  
>out 20H,al 将al中的数据送入到20H号端口中  
>对256 ~ 65535端口中的数据进行读写时，端口号要放在dx中  
>mov dx,3f8h  
>in al,dx   从3f8h端口中读取一个字节，将其放入al中  
>out dx,al  从al中读取一个字节写入3f8h端口中  

### 关于端口在这里总结几点
端口号数无法决定端口位宽，即无法决定这个端口是8位还是16位。  
端口号为160，不代表该端口就是8位，端口号为4556，不代表该端口就是16位。  
判断端口是8位还是16位，关键在于该端口能一次传输一个字节还是两个字节。  

#### 为什么253 ~65535端口号必须用dx存储？
因为out指令或者in指令采用直接端口寻址，寻址范围只有0 ~255。  
要想让in或者out指令访问的端口地址大于255，只能采用间接端口寻址的方式，即用dx存储端口号。  
## CMOS RAM芯片
PC机有一个CMOS RAM芯片，其有如下特征：  
>(1)包含一个实时钟和一个有128个存储单元的RAM存储器  
(2)该芯片靠电池供电。因此，关机后其内部的实时钟仍可正常工作，RAM中的信息不丢失。  
(3)128个字节的RAM中，内部实时钟占用0 ~ 0Dh来保存时间信息，其余大部分单元用于保存系统配置信息，供启动时的BIOS程序读取。BIOS也提供了相关的程序，使我们在开机的时候配置CMOS RAM的中的系统信息。  
(4)该芯片内部有两个端口，端口地址为70H和71H.CPU通过这两个端口读写CMOS RAM。  
(5)70H为地址端口，存放要访问的CMOS RAM单元的地址；71H为数据端口存放从选定的CMOS RAM单元中读取的数据，或要写入其中的数据。  

可见，CPU对CMOS RAM的读写分两步进行。  
比如：读CMOS RAM的2号单元：  
1.将2号送入到端口70H  
2.从71H读出2号单元的内容  
汇编实现:  
mov al,2  
out 70H,2  
in al,71H  
## shl和shr指令
shl和shr是逻辑移位指令。
### shl
逻辑左移指令，功能为：  
>将一个寄存器或内存单元中的数据向左移位。  
>将最后移出的一位写入到CF中  
>最低位用0补充  

举例：  
mov al,01001000B  
shl al,1  
执行后(al)=10010000B,CF=0  
如果移动位数大于1，必须将移动位数放在CL中。  
### shr
逻辑右移指令，功能为：  
>将一个寄存器或内存单元中的数据向右移位。  
>将最后移出的一位写入到CF中  
>最高位用0补充  

如果移动位数大于1，必须将移动位数放在CL中。  
## CMOS RAM中存储的时间信息
在CMOS RAM中，存放着当前时间：年月日时分秒，这六个信息的长度都为一个字节，存放单元依次为：9，8，7，4，6，0.  
这些数据均以BCD码的方式存放。  
BCD码是以4位二进制码表示十进制数的编码方式。  
举例：14的 BCD码：0001(十进制1)   0100(十进制4)  
可见CMOS RAM存放时间的内存单元中存储了两个BCD码，高四位表示十位，低四位表示个位。  
### 编程：在屏幕中间显示当前月份
![contents](https://github.com/MzjHarley/AssemblyLanguageBasicKnowledge/blob/main/img/%E6%B1%87%E7%BC%96%E8%AF%AD%E8%A8%80%E4%B9%8B%E7%AB%AF%E5%8F%A3/1.png)  
运行结果：  
![contents](https://github.com/MzjHarley/AssemblyLanguageBasicKnowledge/blob/main/img/%E6%B1%87%E7%BC%96%E8%AF%AD%E8%A8%80%E4%B9%8B%E7%AB%AF%E5%8F%A3/2.png)
## 实验14
编程：以'''年/月/日 时:分:秒'的格式，显示当前的日期，时间。  
```assembly
assume cs:code

data segment
db 9,8,7,4,6,0
db '2000/00/00 00:00:00'
data ends

code segment
start:
       mov ax,data
       mov ds,ax
       mov di,0
       mov si,8

       mov cx,6
s:     
       push cx
       mov al,[di]
       out 70H,al
       in al,71H
       mov ah,al
       and ah,00001111B
       mov cl,4
       shr al,cl
       add ah,30H
       add al,30H
      
       mov [si],ax
       inc di
       add si,3
       pop cx
loop s
       mov ax,0B800H
       mov es,ax
       mov cx,19
       mov si,160*12+40*2
       mov di,6
s1:
       mov al,[di]
       mov es:[si],al
       inc di
       add si,2
loop s1

       mov ax,4c00H
       int 21H
code ends
end start
```
运行结果：  
![contents](https://github.com/MzjHarley/AssemblyLanguageBasicKnowledge/blob/main/img/%E6%B1%87%E7%BC%96%E8%AF%AD%E8%A8%80%E4%B9%8B%E7%AB%AF%E5%8F%A3/3.png)
# 端口结束