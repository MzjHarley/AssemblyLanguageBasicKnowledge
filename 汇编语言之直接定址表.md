---
title: 汇编语言之直接定址表
date: 2021-09-11 09:14:17
author: MZJ
---
# 直接定址表
## 描述单元长度的标号
### 下面的程序将code段中的a标号处的8个数据累加，结果存储在b标号出的字。
```assembly
assume cs:code
code segment
a: db 1,2,3,4,5,6,7,8
b: dw 0
start:
      mov si,offset a
      mov bx,offset b
      mov cx,8
    s:
      mov al,cs:[si]
      mov ah,0
      add cs:[bx],ax
      inc si
      loop s
      mov ax,4C00H
      int 21H
code ends
end start
```
程序中的code、a、b、start、s都是标号。这些标号仅仅保存了内存单元的地址。  
我们还可以使用另外一种标号，这种标号不仅可以表示内存单元的地址，还可以表示内存单元的长度，即在此标号处的单元是一个字节单元，还是字单元，还是双字单元。
```assembly
assume cs:code
code segment
a db 1,2,3,4,5,6,7,8
b dw 0
start:
      mov si,0
      mov cx,8
    s:
      mov al,a[si]
      mov ah,0
      add b,ax
      inc si
      loop s
      mov ax,4C00H
      int 21H
code ends
end start
```
对比两个程序，我们发现第二个程序在code段中使用的标号a、b后面没有":",它们是同时内存单元和单元长度的标号。标号a，描述了code:0，从这个地址开始，以后的内存单元都是字节单元。  
因为这种标号包含了对单元长度的描述，所以在指令中它可以代表一个段中的内存单元。  
使用这种包含单元长度的标号，可以使我们以简洁的形式访问内存中的数据。  
**以后我们将这种标号称为数据标号。它记录的存储单元的地址和长度，可以代表内存单元。**
## 在其他段中使用数据标号
在其他段中也可以使用数据标号来描述存储单元的地址和长度。在后面加有‘’‘:’的地址标号只能在代码段中使用。  
如果想在代码段中，直接用数据标号访问其他段中的数据，则需要用伪指令assume将标号所在的段和一个段寄存器联系起来，否则在编译器编译时无法确定标号所在的段地址在哪一个寄存器中。  
这种联系是编译器需要的，但不会因为编译器的工作需要用assume将段寄存器和某个段相联系，段寄存器就真的存放该段的地址，我们需要自己设置。  

>我们可以将标号当作数据来定义，此时编译器将标号所在的地址当作数据的值。  
>data segment  
a db 1,2,3,4,5,6,7,8  
b dw 0  
c dw a,b  
data ends  
此时标号c处存储的两个字型数据为offset a和offset b  
>  
>data segment  
a db 1,2,3,4,5,6,7,8  
b dw 0  
c dd a,b  
data ends  
此时标号c处存储的两个双字型数据为a的偏移地址和段地址，b的偏移地址和段地址。  

seg操作符，功能为取得某一标号的段地址。  
## 直接定址表
### 任务:编写子程序show_byte，以16进制的形式在屏幕中间显示给定的byte型数据
```assembly
;al存放要显示的数据
show_byte:
        jmp short show
        table db '0123456789ABCDEF'     ;字符表
   show:
        push bx
        push es
        ;========找出所给byte型数据的高四位和低四位==========
        mov bl,al
        mov bh,bl
        mov cl,4
        shr bh,cl                    ;bh中存放高四位
        and bl,00001111B             ;bl中存放低四位
        ;===============================================
        mov ax,0B800H
        mov es,ax

        mov al,bh
        mov bh,0
        mov ah,table[bx]
        mov es:[12*160+40*2+2],ah

        mov bl,al
        mov ah,table[bx]
        mov es:[12*160+40*2],ah
        ;===============================================
        pop es
        pop bx
        ret
```
上述中我们利用S表，在两个数据集合(二进制和十六进制)之间建立了一种映射关系，使我们用查表的方式根据给出的数据得到其在另一集合中的对应数据。  
>这样做的目的:  
>1.为了算法的简洁和清晰  
>2.为了加块运算速度  
>3.为了使程序易于扩充  

### 任务:编写子程序show_sin，计算sin(x),x∈{0°,30°,60°,90°,120°,150°,180°},并在在屏幕中间显示结果
```assembly
;ax存放角度
show_sin:
         jmp short show
         table dw ag0,ag30,ag60,ag90,ag120,ag150,ag180 ;字符串偏移地址表
         ag0   db '0',0 ;以0结尾表示字符串结束
         ag30  db '0.5',0
         ag60  db '0.866',0
         ag90  db '1',0
         ag120 db '0.866',0
         ag150 db '0.5',0
         ag180 db '0',0
   show: 
        push bx
        push si
        push es
        ;====计算sin(角度)在字符串偏移地址表中的位置=====
         mov ah,0
         mov bl,30
         div bl
         mov bl,al
         mov bh,0
         add bx,bx
         mov bx,table[bx]    ;bx中存放的是对应字符串偏移地址
         ;==========================================
         mov ax,0B800H
         mov es,ax
         
         mov si,12*160+40*2
         ;将字符串放入内存，字符'0'的十进制为48，字符'.'的十进制为46
   shows:mov ah,cs:[bx]
         cmp ah,0
         je showret
         mov es:[si],ah
         inc bx
         add si,2
         jmp short shows
         ;==========================================
 showret:pop es
         pop si
         pop bx
         ret
```
### 总结
上述两个子程序，都采用了建立映射关系的方式来解决问题，**本质上是用空间换时间**。  
编程要注意程序的容错性，在程序中我们还要对错误输入进行判断并进行处理，从而增加程序的可用性。  
上述程序，我们将通过给出数据计算或比较得出的的问题，转化为查表的方式解决问题。  
像这种可以通过依据数据，直接计算出所要找的元素的位置的表，我们称其为直接定址表。
## 程序入口的直接定址表
我们可以将子程序的地址存入直接定址表，从而方便地实现不同子程序的调用。
### 实现一个子程序set_screen
>功能如下:  
>1.清屏  
>2.设置前景色  
>3.设置背景色  
>4.向上滚动一行  
>参数说明:  
>用ah寄存器传递功能号，0表示清屏，1表示设置前景色，2表示设置背景色，3表示向上滚动一行  
>对1，2号功能，用al传递颜色值，(al)∈{0,1,2,3,4,5,6,7}  

```assembly
set_screen:
           jmp short set
           table dw sub1,sub2,sub3,sub4
           
       set:push bx
       
           cmp ah,3
           ja sret
           mov bh,0
           mov bl,ah
           add bx,bx
           mov bx,table[bx]
           call word ptr cs:[bx]
           
      sret:pop bx
           ret
;================================================
sub1:      push es
           push bx
           push cx
           
           mov bx,0B800H
           mov es,bx
           mov bx,0
           mov cx,2000
     sub1s:mov byte ptr es:[bx],' '
           add bx,2
           loop sub1s
           
           pop cx
           pop bx
           pop es
           ret
;================================================
sub2:      push es
           push cx
           push bx
           
           mov bx,0B800H
           mov es,bx
           mov bx,1
           mov cx,2000
     sub2s:and byte ptr es:[bx],11111000B
           or es:[bx],al
           add bx,2
           loop sub2s
           
           pop bx
           pop cx
           pop es
           ret
;================================================
sub3:      push es
           push cx
           push bx
           
           mov cl,4
           shl al,cl
           mov bx,0B800H
           mov es,bx
           mov bx,1
           mov cx,2000
     sub3s:and byte ptr es:[bx],10001111B
           or es:[bx],al
           add bx,2
           loop sub3s
           
           pop bx
           pop cx
           pop es
           ret
;================================================
sub4:      push es
           push cx
           push si
           push di
           push ds
           
           mov ax,0B800H
           mov es,ax
           mov ds,ax
           mov si,160
           mov di,0
           mov cx,24
           cld
      sub4s:
           push cx
           mov cx,160
           rep movsb
           pop cx
           loop sub4s
           
           mov cx,80
           mov si,0
    sub4s1:mov byte ptr es:[si+24*160],' ' ;最后一行清空
           add si,2
           loop sub4s1
           
           pop ds
           pop di
           pop si
           pop cx
           pop es
           ret
```
## 实验16 编写多个包含子程序的中断例程
安装一个新的int 7CH中断例程，为显示输出提供如下功能:  
>功能如下:  
>1.清屏  
>2.设置前景色  
>3.设置背景色  
>4.向上滚动一行  
>参数说明:  
>用ah寄存器传递功能号，0表示清屏，1表示设置前景色，2表示设置背景色，3表示向上滚动一行  
>对1，2号功能，用al传递颜色值，(al)∈{0,1,2,3,4,5,6,7}  

```assembly
assume cs:code
code segment
start:
;=====================安装int7ch中断例程======================
mov ax,0
mov es,ax
mov di,200H
mov ax,cs
mov ds,ax
mov si,offset int7cH
mov cx,offset int7cH_end - offset int7cH
cld
rep movsb
;=======================登记int7ch中断例程===================
mov word ptr es:[4*7ch],200H
mov word ptr es:[4*7ch+2],0
;==========================返回==========================
mov ax,4c00H
int 21H
;===========================编写int7ch======================
int7cH:
           jmp short int7cH_begin
           table dw offset sub1- offset int7cH+200h
                 dw offset sub2- offset int7cH+200h
                 dw offset sub3- offset int7cH+200h
                 dw offset sub4- offset int7cH+200h
           
int7cH_begin:
           push bx
       
           cmp ah,3
           ja int7cHret
           mov bh,0
           mov bl,ah
           add bx,bx
           call word ptr cs:[bx+ offset table - offset int7cH+200H]
           
 int7cHret:
           pop bx
           iret
;================================================
   sub1:   
          push es
           push bx
           push cx
           
           mov bx,0B800H
           mov es,bx
           mov bx,0
           mov cx,2000
     sub1s:
           mov byte ptr es:[bx],' '
           add bx,2
           loop sub1s
           
           pop cx
           pop bx
           pop es
           ret
;================================================
   sub2:   
           push es
           push cx
           push bx
           
           mov bx,0B800H
           mov es,bx
           mov bx,1
           mov cx,2000
     sub2s:
           and byte ptr es:[bx],11111000B
           or es:[bx],al
           add bx,2
           loop sub2s
           
           pop bx
           pop cx
           pop es
           ret
;================================================
   sub3:   
           push es
           push cx
           push bx
           
           mov cl,4
           shl al,cl
           mov bx,0B800H
           mov es,bx
           mov bx,1
           mov cx,2000
     sub3s:
           and byte ptr es:[bx],10001111B
           or es:[bx],al
           add bx,2
           loop sub3s
           
           pop bx
           pop cx
           pop es
           ret
;================================================
   sub4:   
           push es
           push cx
           push si
           push di
           push ds
           
           mov ax,0B800H
           mov es,ax
           mov ds,ax
           mov si,160
           mov di,0
           mov cx,24
           cld
      sub4s:
           push cx
           mov cx,160
           rep movsb
           pop cx
           loop sub4s
           
           mov cx,80
           mov si,0
    sub4s1:
           mov byte ptr es:[si+24*160],' ' ;最后一行清空
           add si,2
           loop sub4s1
           
           pop ds
           pop di
           pop si
           pop cx
           pop es
           ret
;================================================
int7cH_end: nop
;===========================================================
code ends
end start
```
### 新的int 7CH中断例程测试程序
```assembly
assume cs:code
code segment
start:
mov ah,0
mov al,2
int 7CH

mov ax,4c00H
int 21H	
code ends
end start
```
这里你可以为ah赋值0，1，2，3来测试。
### 实验16注意
标号在编译时，就被编译成一个固定的值了。安装到0:200H后，如果仍然按照table[bx]，会出现错误。
# 直接定址表结束